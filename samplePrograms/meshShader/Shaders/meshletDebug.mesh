#version 460

#extension GL_EXT_mesh_shader : require
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable

#include "common.glsl"

layout(binding = 0, set = 0) uniform UniformBuffer {Ubo ubo;};
layout(binding = 3, set = 0, scalar) readonly buffer VertexBuffer {vec3 verts[];};
layout(binding = 7, set = 0) readonly buffer MeshletVerticesBuffer {uint meshletVertices[];};
layout(binding = 8, set = 0, scalar) readonly buffer MeshletTrianglesBuffer {uint8_t meshletTriangles[];};
layout(binding = 9, set = 0, scalar) readonly buffer MeshletBuffer {Meshlet meshlets[];};

layout(local_size_x = meshShadersPerMeshlet, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = MAX_MESHLET_VERTICES, max_primitives = MAX_MESHLET_TRIANGLES) out;

layout (location = 0) out flat vec3 fragColor[];

taskPayloadSharedEXT TaskPayload taskPayload;

float randomFloat(inout uint state) {
    state = state * 747796405 + 2891336453;
    uint result = ((state >> ((state >> 28) + 4)) ^ state) * 277803737;
    result = (result >> 22) ^ result;
    return result / 4294967295.0;
}

void main()
{
    const uint threadIdx = gl_LocalInvocationID.x;
    const Meshlet meshlet = meshlets[taskPayload.meshletIndices[gl_WorkGroupID.x]];
    if (threadIdx == 0) SetMeshOutputsEXT(uint(meshlet.vertexCount), uint(meshlet.triangleCount));

    const mat4 projViewModelMat = ubo.projectionMatrix * ubo.viewMatrix * taskPayload.modelMatrix;

    uint seed = taskPayload.meshletIndices[gl_WorkGroupID.x];
    for (uint i = threadIdx; i < uint(meshlet.vertexCount); i += meshShadersPerMeshlet) {
        const uint idx = meshletVertices[meshlet.vertexOffset + i];
        fragColor[i] = vec3(randomFloat(seed), randomFloat(seed), randomFloat(seed));
        gl_MeshVerticesEXT[i].gl_Position = projViewModelMat * vec4(verts[idx], 1.0);
    }

    for (uint i = threadIdx; i < uint(meshlet.triangleCount); i += meshShadersPerMeshlet) {
        gl_PrimitiveTriangleIndicesEXT[i] = uvec3(meshletTriangles[meshlet.triangleOffset + i * 3],
            meshletTriangles[meshlet.triangleOffset + i * 3 + 1], meshletTriangles[meshlet.triangleOffset + i * 3 + 2]);
    }
}
